/*
 * Copyright (c) 2012, INSA of Rennes
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the INSA of Rennes nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */

// Author: kdesnos (kdesnos@insa-rennes.fr)
// Actor that inputs the Y component of a frame and
// splits it into overlapping slices. 

package org.ietr.sobel.sdf;

actor SplitImage (int NB_SLICES = 12, int WIDTH= 640, int HEIGHT = 480) int(size=8) frame ==> int(size=8) slices_out :
	
    // Constant definition - Removed because of strange behavior :
	// Value cannot be different from default in C backend
	// and considered as state variable in Embedded-C backend
	//int FRAME_SIZE = WIDTH*HEIGHT;
	//int SLICE_SIZE = WIDTH*HEIGHT/NB_SLICES + WIDTH*2;
	
	split: action frame:[ frame_in ] repeat WIDTH*HEIGHT // FRAME_SIZE
			==> slices_out:[ s_out ] repeat WIDTH*(HEIGHT + NB_SLICES*2) // SLICE_SIZE*NB_SLICES
	var
		int(size=8) s_out [ WIDTH*(HEIGHT + NB_SLICES*2) ],
		int SLICE_HEIGHT = HEIGHT/NB_SLICES + 2,
		int NB_OUTPUT_LINES = HEIGHT + NB_SLICES*2,
		int currentSlice,
		int copiedLine
		
	do
		// First and last lines are filled with 0
		foreach int i in 0 .. WIDTH do
			s_out[i] := 0;
			s_out[(NB_OUTPUT_LINES-1)*WIDTH + i] := 0;
		end
		
		// Create the slices
		foreach int outputLine in 1 .. NB_OUTPUT_LINES-2 do
			// Matches the line of the output with a line of the input
			// Since slices overlap, some line will be copied twice 
			
			// More precisely, the last line of the previous slice
			// and the first line of the next are copied to the current slice
			currentSlice := outputLine div SLICE_HEIGHT; // integer division
			copiedLine := outputLine - 1 - 2*currentSlice;
			
			// Copy the line
			foreach int pixel in 0 .. WIDTH-1 do
				s_out[outputLine*WIDTH + pixel] := frame_in[copiedLine*WIDTH + pixel]; 
			end
		end
	end
end